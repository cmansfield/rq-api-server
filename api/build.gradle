plugins {
    id 'project-conventions'
    id 'groovy'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'

    implementation project(':server')
    testImplementation 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/unit/mockito', 'src/test/integration']
        }
        groovy {
            srcDirs = ['src/test/unit/groovy']
        }
    }
}

test {
    testLogging {
        debug {
            events 'passed', 'skipped', 'failed'
        }
        
        info {
            events 'failed', 'skipped'
        }
    }

    if (System.properties['test.profile'] == 'integration') {
        exclude '**/*Test.class'
        include '**/**IT.class'
    } else if (System.properties['test.profile'] == 'all') {
        include '**'
    } else {
        include '**'
        exclude '**/**IT.class'
    }
}

springBoot {
    mainClass = 'com.reliaquest.api.ApiApplication'
}